dnl configure.in, based on configure.scan from 'autoscan'
dnl dugsong@monkey.org and provos@citi.umich.edu

AC_INIT(outguess.c)

AC_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC
dnl Check for some compiler options
if test -n "$GCC"; then
   AC_MSG_CHECKING([if the compiler understands -pipe -Wall -funroll-all-loops])
   OLDCC="$CC"
   CC="$CC -pipe -Wall -funroll-all-loops"
   AC_TRY_COMPILE(,,
      AC_MSG_RESULT(yes),
      CC="$OLDCC"
      AC_MSG_RESULT(no))
fi
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h unistd.h md5.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T
AC_CHECK_TYPE(u_int64_t, unsigned long long)
AC_CHECK_TYPE(u_int32_t, unsigned int)
AC_CHECK_TYPE(u_int16_t, unsigned short)
AC_CHECK_TYPE(u_int8_t, unsigned char)

dnl Checks for library functions.
AC_FUNC_MMAP
AC_CHECK_FUNCS(strrchr memcpy)

AC_CHECK_FUNC(strcasecmp, ,
	AC_MSG_RESULT(File extensions will be case sensitive!)
	     )

AC_CHECK_FUNC(snprintf, ,
	AC_MSG_RESULT(will use sprintf instead you have a bad system)
	     )

dnl Check if we need to compile md5c
AC_SUBST(MD5OBJ)
AC_CHECK_FUNC(MD5Init, , CFLAGS="$CFLAGS -I."; MD5OBJ="md5c.o")

AC_CONFIG_SUBDIRS(jpeg-6b-steg)

AC_OUTPUT(Makefile)
